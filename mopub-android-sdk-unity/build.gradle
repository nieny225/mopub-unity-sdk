apply plugin: 'com.android.library'

project.version = '5.6.0'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'
        classpath 'com.novoda:bintray-release:0.9'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1'
}

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'libs'
    }
    maven { url 'https://maven.google.com' } // necessary for Android API 26
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionName project.version
        consumerProguardFiles 'proguard.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard.txt')
        }

        debug {
            minifyEnabled false
        }
    }
}

dependencies {
    api (project(":mopub-sdk")) {
        transitive = true
        // Viewability measurement is not supported on the MoPub Unity Plugin yet
        exclude module: 'libAvid-mopub'
        exclude module: 'moat-mobile-app-kit'
    }

    // Unity's jar file is in the libs subdirectory
    compileOnly fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}


allprojects { project ->
    if (project.name != "mopub-sdk" && project.name != "mopub-sdk-native-video") {
        task copyJar(type: Copy, dependsOn: 'compileReleaseJavaWithJavac') {
            from('build/intermediates/packaged-classes/release/') {
                include 'classes.jar'
                rename 'classes.jar', "${project.name}.jar"
            }
            into "$rootDir/../unity-sample-app/Assets/MoPub/Plugins/Android/MoPub.plugin/libs"
        }
        afterEvaluate { assembleRelease.dependsOn(copyJar) }
    }
}
